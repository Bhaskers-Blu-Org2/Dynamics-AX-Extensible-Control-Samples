[FormControlAttribute("BasicUserEntryControl","resources/html/BasicUserEntryControl", classstr(FormBuildControl))]
class BasicUserEntryControl extends FormTemplateControl
{
    #define.ValuePropertyName("Value")
    #define.SetValueCommandName("SetValue")
    FormProperty valueProperty;    

    public void new(FormBuildControl _build, FormRun _formRun)
    {
        super(_build,_formRun);

        this.setTemplateId("BasicUserEntryControl");
        this.setResourceBundleName("resources/html/BasicUserEntryControl");

        valueProperty = this.addProperty(methodStr(BasicUserEntryControl, getValue), Types::String);
    }

    [FormPropertyAttribute(FormPropertyKind::Value, ValuePropertyName, true)]
    private str getValue()
    {
       return valueProperty.parmValue();
    }

    /* 
		In general, a method that is exposed externally via a FormPropertyAttribute or FormCommandAttribute
		should not also be declared public. This is because externally exposed methods usually necessitate
		additional data validation and security that methods called from internal code do not need.
		See Tutorial 5 for an example of how to correctly design this behavior.
    */
    [FormCommandAttribute(SetValueCommandName)]
    public void setValue(str _value = "")
    {
        valueProperty.setValueOrBinding(_value);
        this.properties().notifyChange(valueProperty);  
        info(strFmt("Value set to: %1",_value));   
    }

}

//The following is not a part of the sample code
//vim: set syntax=csharp: